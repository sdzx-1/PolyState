写这个文档比我想象中更困难。虽然我编写并使用了这个库有一段时间，但是想将这一切向他人清晰地阐述并非易事。如果您在阅读文档时有任何疑问，请随时提出，我很乐意为您解答！

## polystate 核心设计理念
1. 在类型上记录状态机的状态。
2. 通过组合类型实现组合状态机。

有限状态机是一种非常强大的编程模式，如果再加上组合性和类型安全，它将成为一种更理想的编程范式。

polystate 库正是为此目标而设计。为了实现这一目标，需要遵循一些简单的编程规范。这些规范非常简单，但它们带来的好处是完全值得的。

## polystate 实际效果
1.  **通过组合式声明确定程序的整体行为**。这意味着我们拥有了在类型级别上定义程序整体行为的能力。这极大地提升了命令式程序结构的正确性，同时这种编程方式也会促使我们从类型和组合的角度重新设计程序的状态，进而提升代码的组合性。
2.  **通过组合简单状态构建复杂状态机**。我们第一次做到了：通过组合类型实现语义级的代码复用，或者说，我们为语义级的代码复用找到了类型上的表达方式。这种做法同时达到了简洁、正确、安全三重效果。
3.  **自动生成状态图**。由于程序的整体行为是由声明确定的，polystate 可以自动生成状态图, 用户通过状态图可以直观地了解程序的整体行为。

我相信这一切代表了命令式编程的伟大进步！

## 详细解释设计理念和实际效果

让我们从一个具体的简单状态机例子开始，我会在代码的注释中详细介绍这个库的核心设计理念。

```zig
const std = @import("std");
const polystate = @import("polystate");

pub fn main() !void {
    var st: GST = .{};
    ///确定一个初始状态
    const wa = Example.Wit(Example.a){};
    ///用这个初始状态的消息处理函数开始执行状态机
    ///至于这里为什么使用handler_normal，这与尾递归优化有关，我会在后面详细解释。
    wa.handler_normal(&st);
}

pub const GST = struct {
    counter_a: i64 = 0,
    counter_b: i64 = 0,
};

///polystate 有两个核心的状态类型：FST（FSM Type），GST（Global State Type）。
///FST 只能是enum类型，这个例子中的FST 是Example，它定义了我们状态机的所有状态，或者说它定义了我们将在类型上跟踪的状态集合。
///GST 是全局数据，这个例子的GST定义在上面，这里有两个字段counter_a, counter_b，分别代表a状态和b状态需要的数据。
///当我们组合状态时，我们其实想要的是组合状态处理函数，而这隐含了对全局数据的要求。
///因此第一个编程规范是：任意一个状态的处理函数都能访问GST（即全局数据），但用户应该尽量只使用与当前状态对应的数据。
///比如这个例子中在a状态的处理函数中应该尽量只使用数据counter_a。
///通过一些命名规范应该能很容易做到这一点，通过元编程应该很容易制作相应的通用函数，但这不属于polystate的范围。
const Example = enum {
    ///这里定义了三个具体状态
    exit,
    a,
    b,

    ///Wit是polystate的核心概念，全称是Witness。这个词来自于[haskell](https://serokell.io/blog/haskell-type-level-witness)，它被称为 'type witness' 或者 'runtime evidence'。
    ///有限状态机的核心概念包含四部分：状态、消息、消息处理函数、消息产生函数。我会在下面的例子中详细介绍这几个部分。
    ///Wit函数的作用是指定消息中包含的状态信息。
    pub fn Wit(val: anytype) type {
        return polystate.Witness(@This(), GST, null, polystate.val_to_sdzx(@This(), val));
    }

    ///这里是第二个编程规范：FST 中需要有包含状态具体内容的公开声明，通过在状态后面加ST来隐式地将状态和状态的具体内容关联起来。
    ///在这个例子中，对应于存在下面的公开声明
    ///exit ~ exitST
    ///a    ~ aST
    ///b    ~ bST
    ///这里exitST 描述了在exit 状态下的四部分内容：状态、消息、消息处理函数、消息产生函数。
    ///由于exit状态下没有任何的消息，因此也没有消息产生函数。
    ///这里是第三个编程规范：状态的具体内容的实现中必须含有函数： pub fn handler(*GST) void 或者  pub fn conthandler(*GST) ContR。
    ///它们表示消息处理函数。这里前者表示状态机完全拥有控制流，后者表示返回一个延续函数，将控制流交由外部调用。
    pub const exitST = union(enum) {
        pub fn handler(ist: *GST) void {
            std.debug.print("exit\n", .{});
            std.debug.print("st: {any}\n", .{ist.*});
        }
    };
    pub const aST = a_st;
    pub const bST = b_st;
};

///这里描述了处于a状态下的四部分内容：状态，消息，消息处理函数，消息产生函数
///1. 状态
///这里的状态是a
pub const a_st = union(enum) {
    ///2. 消息
    ///这里使用tagged union来描述消息，使用Wit描述我们将要跳转的状态
    AddOneThenToB: Example.Wit(Example.b),
    Exit: Example.Wit(Example.exit),

    ///3. 消息处理函数
    ///处理genMsg产生的所有消息
    pub fn handler(ist: *GST) void {
        switch (genMsg(ist)) {
            .AddOneThenToB => |wit| {
                ist.counter_a += 1;
                ///这里是第四个编程规范：在处理消息的语句块最后必须调用 wit.handler(ist) 或其他类似的代码。
                ///这表明将执行新状态的消息处理函数，这里的新状态由消息的Wit函数控制。
                wit.handler(ist);
            },
            .Exit => |wit| wit.handler(ist),
        }
    }

    ///4. 消息产生函数
    ///如果counter_a 的值大于3，返回.Exit
    ///否则返回.AddOneThenToB
    ///这里产生和处理的消息都是由上面2定义
    fn genMsg(ist: *GST) @This() {
        if (ist.counter_a > 3) return .Exit;
        return .AddOneThenToB;
    }
};

pub const b_st = union(enum) {
    AddOneThenToA: Example.Wit(Example.a),

    pub fn handler(ist: *GST) void {
        switch (genMsg()) {
            .AddOneThenToA => |wit| {
                ist.counter_b += 1;
                wit.handler(ist);
            },
        }
    }

    fn genMsg() @This() {
        return .AddOneThenToA;
    }
};

```
以上就是一个简单的例子，展示了如何用polystate 构建一个简单的状态机。
这个例子并没有展示polystate最强大的特性：**组合性**。

## 解释组合性
让我修改上述的例子，加入一个新的状态`yes_or_no`，用它来演示组合性。
我会省略一些与上面相同的代码，这个例子的完整代码可以在[这里找到](https://github.com/sdzx-1/polystate-examples/blob/main/src/exe-counter.zig)。

```zig
const std = @import("std");
const polystate = @import("polystate");

pub fn main() !void {
   ...
}

pub const GST = struct {
  ...
  buf: [10] u8 = @splat(0),
};

///Example
const Example = enum {
    exit,
    a,
    b,
    ///这里定义了新状态yes_or_no
    yes_or_no,



    pub fn Wit(val: anytype) type {
        ...
    }

    pub const exitST = union(enum) {
      ...
    };
    pub const aST = a_st;
    pub const bST = b_st;
    
    ///新状态的具体实现是一个函数，它依赖于两个状态参数：yes 和 no。
    ///它的语义是给用户提供一个交互的选择界面：如果用户选择yes，那么跳转到yes对应的状态；如果用户选择no，则跳转到no对应的状态。
    ///这里的sdzx函数将普通的enum 类型变成一个可以组合的新类型。
    ///比如我可以用：polystate.sdzx(Example).C(.yes_or_no, &.{ .a, .b }) 来表示状态 (yes_or_no, a, b)。
    ///我一般将这种类型写成：yes_or_no(a, b), 它表明yes_or_no是一种特殊的状态，它需要两个具体的状态参数。
    ///按照语义理解 yes_or_no(exit, a) 表达的含义是：在退出前需要用户确认，如果用户选择yes，那么将进入exit状态；如果用户选择no，那么进入a状态。
    ///同样的 yes_or_no(yes_or_no(exit, a), a) 表达的含义是：在退出前需要用户确认两次，用户需要两次都选择yes才能退出。
    ///这就是组合性的含义，请确保自己理解了这一点。
    pub fn yes_or_noST(yes: polystate.sdzx(@This()), no: polystate.sdzx(@This())) type {
        return yes_or_no_st(@This(), yes, no, GST);
    }
};

pub const a_st = union(enum) {
    AddOneThenToB: Example.Wit(Example.b),
    ///这里展示了在代码中如何构建和使用组合消息
    ///如果是组合消息需要放在tuple中，第一个状态是函数，其余的是这个函数的状态参数
    ///这里 .{ Example.yes_or_no, Example.exit, Example.a } 表示状态：yes_or_no(exit, a)
    Exit: Example.Wit(.{ Example.yes_or_no, Example.exit, Example.a }),
    ///同样可以用 .{ Example.yes_or_no, .{Example.yes_or_no, Example.exit, Example.a}, Example.a } 表示状态：yes_or_no(yes_or_no(exit, a), a)
    ...
};

pub const b_st = union(enum) {
  ...
};

///yes_or_no状态具体实现
///首先它是一个函数，它需要FST, GST1, yes, no这四个参数。注意它的实现与Example本身无关，
///这是一个与具体状态机无关的通用实现，你可以在任意的状态机中使用这个代码。
///我依然从四个方面解释这个代码：状态、消息、消息处理函数、消息产生函数
pub fn yes_or_no_st(
    FST: type,
    GST1: type,
    yes: polystate.sdzx(FST),
    no: polystate.sdzx(FST),
) type {
    ///1. 状态
    ///它的具体状态是：polystate.sdzx(FST).C(FST.yes_or_no, &.{ yes, no })
    ///它需要两个参数yes和no，同时需要保证FST一定有yes_or_no状态。
    return union(enum) {
        ///2. 消息
        ///这里有三条消息，特别需要注意的是Retry，它代表输入错误需要重新输入的语义。
        Yes: Wit(yes),
        No: Wit(no),
        ///注意这里构建的状态，它指向了自己。
        Retry: Wit(polystate.sdzx(FST).C(FST.yes_or_no, &.{ yes, no })),

        fn Wit(val: polystate.sdzx(FST)) type {
            return polystate.Witness(FST, GST1, null, val);
        }

        ///3, 消息处理函数
        pub fn handler(gst: *GST1) void {
            switch (genMsg(gst)) {
                .Yes => |wit| wit.handler(gst),
                .No => |wit| wit.handler(gst),
                .Retry => |wit| wit.handler(gst),
            }
        }

        const stdIn = std.io.getStdIn().reader();
        
        ///4, 消息产生函数
        ///从stdIn读入字符串，如果字符串是"y" 则返回消息 .Yes， 如果字符串是"n"，则返回消息 .No。
        ///其他情况则返回 .Retry。
        fn genMsg(gst: *GST) @This() {
            std.debug.print(
                \\Yes Or No:
                \\y={}, n={}
                \\
            ,
                .{ yes, no },
            );

            const st = stdIn.readUntilDelimiter(&gst.buf, '\n') catch |err| {
                std.debug.print("Input error: {any}, retry\n", .{err});
                return .Retry;
            };

            if (std.mem.eql(u8, st, "y")) {
                return .Yes;
            } else if (std.mem.eql(u8, st, "n")) {
                return .No;
            } else {
                std.debug.print("Error input: {s}\n", .{st});
                return .Retry;
            }
        }
    };
}
```
这个例子清晰地展示了如何通过组合类型来达到组合状态机的效果。

## polystate 在实际项目中的效果

### 1. [组合checkPin，表达复杂逻辑](https://github.com/sdzx-1/polystate-examples/blob/main/src/exe-atm.zig)

假设有一个ATM，当我们处于`checkPin`状态时，会要求用户从外部输入Pin。如果Pin正确，那么发送`Successed`消息，进入`success`参数指定的状态；如果错误，则发送`Failed`消息，进入`failed`参数指定的状态。

一个常见的要求是：用户最多尝试输入三次Pin，如果三次都错误，那么就应该退卡并返回到开始的界面。

这里"最多三次"是一个极其重要的安全要求，不应该被轻易改变。

通过组合状态，我们可以很自然地实现这个效果。我们将`checkPin`设计成一个通用状态，然后在状态转移的声明中，通过组合`checkPin`来精确地描述这一业务逻辑。

```zig
  pub fn checkPinST(success: polystate.sdzx(Atm), failed: polystate.sdzx(Atm)) type {
        return union(enum) {
            Successed: polystate.Witness(Atm, GST, null, success),
            Failed: polystate.Witness(Atm, GST, null, failed),

            ...
            ...
        }
  }

    pub const readyST = union(enum) {
        ///通过嵌套声明三次checkPin，保证检查Pin的行为最多发生三次, 这精确地描述了我们需要的行为。
        ///这展示了如何通过组合式声明来确定程序的整体行为。
        InsertCard: Wit(.{ Atm.checkPin, Atm.session, .{ Atm.checkPin, Atm.session, .{ Atm.checkPin, Atm.session, Atm.ready } } }),
        Exit: Wit(.{ Atm.are_you_sure, Atm.exit, Atm.ready }),

        ...
    }

```
我们可以通过状态图直接看到它的整体逻辑, [polystate 可以自动生成这一切](https://github.com/sdzx-1/polystate-examples/blob/fecaffb5b7f5eba09c9428c18f1cd97e7ee09e71/src/exe-atm.zig#L19)。

![atm-graph](https://github.com/sdzx-1/polystate-examples/raw/main/data/atm_graph.svg)

### 2. [选择语义的实现和复用](https://github.com/sdzx-1/ray-game/blob/master/src/select.zig)

我使用raylib实现了一个通用的"选择"语义：通过鼠标进行交互式选择。

选择的具体行为由三个通用状态（`select`、`inside`、`hover`）和相关的消息共同构成。

这些状态和消息实现了：通过鼠标选择一个元素，以及鼠标悬停时应该如何响应。
```zig

pub fn selectST(
    FST: type,
    GST: type,
    enter_fn: ?fn (polystate.sdzx(FST), *GST) void,
    back: polystate.sdzx(FST),
    selected: polystate.sdzx(FST),
) type {
    const cst = polystate.sdzx_to_cst(FST, selected);
    const SDZX = polystate.sdzx(FST);

    return union(enum) {
        // zig fmt: off
        ToBack  : polystate.Witness(FST, GST, enter_fn, back),
        ToInside: polystate.Witness(FST, GST, enter_fn, SDZX.C(FST.inside, &.{ back, selected })),
        // zig fmt: on
       ...
    };
}

pub fn insideST(
    FST: type,
    GST: type,
    enter_fn: ?fn (polystate.sdzx(FST), *GST) void,
    back: polystate.sdzx(FST),
    selected: polystate.sdzx(FST),
) type {
    const cst = polystate.sdzx_to_cst(FST, selected);
    const SDZX = polystate.sdzx(FST);

    return union(enum) {
        // zig fmt: off
        ToBack    : polystate.Witness(FST, GST, enter_fn, back),
        ToOutside : polystate.Witness(FST, GST, enter_fn, SDZX.C(FST.select, &.{ back, selected })),
        ToHover   : polystate.Witness(FST, GST, enter_fn, SDZX.C(FST.hover, &.{ back, selected })),
        ToSelected: polystate.Witness(FST, GST, enter_fn, selected),
        // zig fmt: on
       ...
    };
}

pub fn hoverST(
    FST: type,
    GST: type,
    enter_fn: ?fn (polystate.sdzx(FST), *GST) void,
    back: polystate.sdzx(FST),
    selected: polystate.sdzx(FST),
) type {
    const cst = polystate.sdzx_to_cst(FST, selected);
    const SDZX = polystate.sdzx(FST);

    return union(enum) {
        // zig fmt: off
        ToBack    : polystate.Witness(FST, GST, enter_fn, back),
        ToOutside : polystate.Witness(FST, GST, enter_fn, SDZX.C(FST.select, &.{ back, selected })),
        ToInside  : polystate.Witness(FST, GST, enter_fn, SDZX.C(FST.inside, &.{ back, selected })),
        ToSelected: polystate.Witness(FST, GST, enter_fn, selected),
        // zig fmt: on

       ...
    };
}

```

在[ray-game](https://github.com/sdzx-1/ray-game) 这个项目中，"选择"语义被重用了至少八次，这极大地减少了代码量，提高了正确性。

这个项目中一个有趣的例子是"两阶段选择"：你需要先选择一个建筑，再选择一个网格位置来放置它，同时建筑的选择会限制位置的选择。![select_twict](data/select_twice.gif)

这样的语义可以简洁地表达为：
```zig
pub const placeST = union(enum) {
    ToPlay: Wit(.{ Example.select, Example.play, .{ Example.select, Example.play, Example.place } }),
    ...
};

```
这段代码极其简洁地描述了我们的意图。但如果你查看状态图，会发现它实际对应的状态转换相当复杂。

![graph](data/graph.png)

通过简单的声明，我们嵌套地复用了复杂的"选择"语义，这是巨大的胜利！

[这一切的完整代码就在这里](https://github.com/sdzx-1/ray-game/blob/587f1698cb717c393c3680060a057ac8b02d89c2/src/play.zig#L33)，大约130行代码。

